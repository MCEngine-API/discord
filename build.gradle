plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

// Set project version using a property, defaulting to 'unspecified' if not found
version = project.properties[project.name + '.version'] ?: 'undefined'
group = 'io.github.mcengine'

ext {
    // User
    git_user_name = System.getenv('GIT_USER_NAME') ?: 'null'
    git_user_token = System.getenv('GIT_USER_TOKEN') ?: 'null'

    // Organization
    git_organization_name = System.getenv('GIT_ORGANIZATION_NAME') ?: 'MCEngine'
    git_organization_token = System.getenv('GIT_ORGANIZATION_TOKEN') ?: 'null'

    // Repository
    git_repository_name = System.getenv('GIT_REPOSITORY_NAME') ?: 'discord'
    git_repository_token = System.getenv('GIT_REPOSITORY_TOKEN') ?: 'null'

    /* Project Information */
    jarName = System.getenv('PROJECT_NAME') ?: 'MCEngineDiscord'
}

allprojects {
    group = 'io.github.mcengine'

    apply plugin: 'java'
    apply plugin: 'com.github.johnrengelman.shadow'

    repositories {
        mavenCentral()
        mavenLocal()

        maven {
            url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots'
            content {
                includeGroup 'org.bukkit'
                includeGroup 'org.spigotmc'
            }
        }

        maven {
            name = "papermc"
            url = 'https://repo.papermc.io/repository/maven-public/'
        }

        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        maven { url 'https://oss.sonatype.org/content/repositories/central' }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'com.github.johnrengelman.shadow'

    version = project.properties[project.name + '.version'] ?: 'undefined'

    tasks.named('jar').configure {
        enabled = false
    }

    tasks.named('shadowJar').configure {
        archiveClassifier = null
    }

    if (project.path == ":mcengine-${project.git_repository_name}-api") {
        apply plugin: 'maven-publish'

        publishing {
            repositories {
                maven {
                    name = 'GitHubPackages'
                    url = uri("https://maven.pkg.github.com/${project.git_organization_name}/${project.git_repository_name}")
                    credentials {
                        username = project.git_user_name
                        password = project.git_user_token
                                ?: project.git_repository_token
                                ?: project.git_organization_token
                    }
                }
            }
            publications {
                mavenJava(MavenPublication) {
                    artifact(tasks.named('shadowJar').get())

                    pom {
                        name.set(project.jarName)
                        description.set("The ${project.jarName} API is designed for use by addons.")
                        url.set("https://github.com/${project.git_organization_name}/${project.git_repository_name}")

                        scm {
                            connection.set("scm:git:git://github.com/${project.git_organization_name}/${project.git_repository_name}.git")
                            developerConnection.set("scm:git:ssh://github.com:${project.git_organization_name}/${project.git_repository_name}.git")
                            url.set("https://github.com/${project.git_organization_name}/${project.git_repository_name}")
                        }

                        developers {
                            developer {
                                id.set('jetsadawijit')
                                name.set('Jetsada Wijit')
                                email.set('jetsadawijit@outlook.com')
                                url.set('https://jetsadawijit.github.io')
                            }
                        }
                    }
                }
            }
        }
    }
}

dependencies {
    implementation project(path: ":mcengine-${project.git_repository_name}-api", configuration: 'shadow')
}

shadowJar {
    version = version
    archiveClassifier = null
    archiveBaseName = project.jarName
}

tasks.named('jar').configure {
    enabled = false
}

build.dependsOn(shadowJar)